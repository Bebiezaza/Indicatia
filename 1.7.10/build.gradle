buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "cursegradle"
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.0.9'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.9'
}

def apiJson = file('M:/Modding/indicatia_api_token.json')
def parsedApi = new groovy.json.JsonSlurper().parseText(apiJson.text)
def mcVersion = '1.7.10'

apply plugin: 'forge'
version = "1.1.2"
group = "stevekung.mods.indicatia"
archivesBaseName = "Indicatia-All"
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"
}

curseforge {
    apiKey = parsedApi.apiKey
    project {
        id = '272146'
        changelog = 'To read full change log use command /inchangelog in-game.'
        releaseType = 'release'
        addGameVersion mcVersion
        addGameVersion 'Java 8'

        mainArtifact(file('build/libs/Indicatia-' + mcVersion + '-' + version + '.jar')) {
            displayName = "Indicatia-" + mcVersion + "-" + version
        }
    }
}

ext.coreJar = "build/libs/" + archivesBaseName + "-" + version + ".jar"
ext.coreFiles = ['stevekung/mods/indicatia/**', 'assets/indicatia/**', 'license.txt', 'META-INF/indicatia_at.cfg']

// build normal jar
task buildJar(type: Jar, dependsOn: "rePackAllJar") {
    outputs.upToDateWhen { false }
    baseName = "Indicatia-" + mcVersion

    from(zipTree(project.ext.coreJar)) {
        includes.addAll(project.ext.coreFiles)
    }
    manifest {
        attributes.put("FMLAT", "indicatia_at.cfg")
        attributes.put("FMLCorePluginContainsFMLMod", "true")
    }
}

task rePackAllJar(type: Jar, dependsOn: "copyModFiles") {
    from 'temporary'
}

task copyModFiles(type: Copy, dependsOn: "reobf") {
    from(zipTree(project.ext.coreJar))
    into 'temporary'
}

// build deobf jar
task buildDeobfJar(type: Jar, dependsOn: "rePackAllDeobfJar") {
    outputs.upToDateWhen { false }
    baseName = "Indicatia-" + mcVersion
    classifier = 'deobf'

    from(zipTree(project.ext.coreJar)) {
        includes.addAll(project.ext.coreFiles)
    }
    manifest {
        attributes.put("FMLAT", "indicatia_at.cfg")
        attributes.put("FMLCorePluginContainsFMLMod", "true")
    }
}

task rePackAllDeobfJar(type: Jar, dependsOn: "copyDeobfModFiles") {
    from 'temporary_deobf'
}

task copyDeobfModFiles(type: Copy, dependsOn: "reobf") {
    from sourceSets.main.output
    into 'temporary_deobf'
}

// delete temp folders
task deleteTemporary(type: Delete) {
    delete 'temporary'
    delete 'temporary_deobf'
}

buildJar.finalizedBy(deleteTemporary)
buildDeobfJar.finalizedBy(deleteTemporary)