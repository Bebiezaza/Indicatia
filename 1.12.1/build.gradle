buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.0.9'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.9'
}

def apiJson = file('M:/Modding/indicatia_api_token.json')
def parsedApi = new groovy.json.JsonSlurper().parseText(apiJson.text)
def mcVersion = '1.12.1'

apply plugin: 'net.minecraftforge.gradle.forge'
version = "1.1.1"
group = "stevekung.mods.indicatia"
archivesBaseName = "Indicatia-All"
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.12.1-14.22.0.2459"
    runDir = "run"
    mappings = "snapshot_20170624"
    makeObfSourceJar = false
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

curseforge {
    apiKey = parsedApi.apiKey
    project {
        id = '272146'
        changelog = 'To read full change log use command /inchangelog in-game.'
        releaseType = 'release'
        addGameVersion '1.12'
        addGameVersion mcVersion

        mainArtifact(file('build/libs/Indicatia-' + mcVersion + '-' + version + '.jar')) {
            displayName = "Indicatia-" + mcVersion + "-" + version
        }
    }
}

ext.coreJar = "build/libs/" + archivesBaseName + "-" + version + ".jar"
ext.coreFiles = ['stevekung/mods/indicatia/**', 'assets/indicatia/**', 'license.txt', 'META-INF/indicatia_at.cfg']
ext.internalFiles = ['stevekung/mods/indicatia/internal/**', 'assets/indicatia/sounds.json', 'assets/indicatia/sounds/**']

// build normal jar
task buildJar(type: Jar, dependsOn: "rePackAllJar") {
    outputs.upToDateWhen { false }
    baseName = "Indicatia-" + mcVersion

    from(zipTree(project.ext.coreJar)) {
        includes.addAll(project.ext.coreFiles)
        excludes.addAll(project.ext.internalFiles)
    }
    manifest {
        attributes.put("FMLAT", "indicatia_at.cfg")
        attributes.put("FMLCorePluginContainsFMLMod", "true")
    }
}

task rePackAllJar(type: Jar, dependsOn: "copyModFiles") {
    from 'temporary'
}

task copyModFiles(type: Copy, dependsOn: "reobfJar") {
    from(zipTree(project.ext.coreJar))
    into 'temporary'
}

// build my jar
task buildMyJar(type: Jar, dependsOn: "rePackAllJar") {
    outputs.upToDateWhen { false }
    baseName = "Indicatia-" + mcVersion
    classifier = 'me'

    from(zipTree(project.ext.coreJar)) {
        includes.addAll(project.ext.coreFiles)
        includes.addAll(project.ext.internalFiles)
    }
    manifest {
        attributes.put("FMLAT", "indicatia_at.cfg")
        attributes.put("FMLCorePluginContainsFMLMod", "true")
    }
}

// build deobf jar
task buildDeobfJar(type: Jar, dependsOn: "rePackAllDeobfJar") {
    outputs.upToDateWhen { false }
    baseName = "Indicatia-" + mcVersion
    classifier = 'deobf'

    from(zipTree(project.ext.coreJar)) {
        includes.addAll(project.ext.coreFiles)
    }
    manifest {
        attributes.put("FMLAT", "indicatia_at.cfg")
        attributes.put("FMLCorePluginContainsFMLMod", "true")
    }
}

task rePackAllDeobfJar(type: Jar, dependsOn: "copyDeobfModFiles") {
    from 'temporary_deobf'
}

task copyDeobfModFiles(type: Copy, dependsOn: "reobfJar") {
    from sourceSets.main.output
    into 'temporary_deobf'
}

// delete temp folders
task deleteTemporary(type: Delete) {
    delete 'temporary'
    delete 'temporary_deobf'
}

buildJar.finalizedBy(deleteTemporary)
buildDeobfJar.finalizedBy(deleteTemporary)