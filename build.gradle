buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.0.9'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.9'
}

def api = new groovy.json.JsonSlurper().parseText(file("${api_token_path}").text)
def keystore = new groovy.json.JsonSlurper().parseText(file("${keystore_path}").text)
def mcVersion = project.mc_version

apply plugin: 'net.minecraftforge.gradle.forge'
version = project.version
group = "stevekung.mods.indicatia"
archivesBaseName = "Indicatia-" + mcVersion
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.10.2-12.18.3.2511"
    runDir = "run"
    mappings = "snapshot_20161111"
    makeObfSourceJar = false

    replace '@FINGERPRINT@', keystore.sha1
    replaceIn "IndicatiaMod.java"
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

jar
{
    manifest {
        attributes.put("FMLAT", "indicatia_at.cfg")
        attributes.put("FMLCorePluginContainsFMLMod", "true")
    }
}

curseforge {
    apiKey = api.indicatia
    project {
        id = '272146'
        changelog = 'To read full change log use command /inchangelog in-game.\n\n or follow this link: https://raw.githubusercontent.com/SteveKunG/Indicatia/master/' + mcVersion + '/src/main/resources/assets/indicatia/change_log.txt'
        releaseType = 'release'
        addGameVersion mcVersion

        mainArtifact(file('build/libs/Indicatia-' + mcVersion + '-' + version + '.jar')) {
            displayName = "Indicatia-" + mcVersion + "-" + version
        }
    }
}

task buildDeobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {
        attributes.put("FMLAT", "indicatia_at.cfg")
        attributes.put("FMLCorePluginContainsFMLMod", "true")
    }
}

task signJar(type: SignJar, dependsOn: "reobfJar") {
    keyStore = keystore.keystore_path
    alias = keystore.alias
    storePass = keystore.keystore_pass
    keyPass = keystore.keystore_pass
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar