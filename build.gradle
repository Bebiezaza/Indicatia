buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/maven" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

def mcVersion = project.mc_version

apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"
apply plugin: "eclipse"
apply plugin: "maven-publish"

import net.minecraftforge.gradle.common.task.SignJar

version = project.version
group = "com.stevekung.indicatia"
archivesBaseName = "Indicatia-All"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 1.8

minecraft {
    mappings channel: "snapshot", version: "${mcp_mapping}"
    accessTransformer = file("$rootDir/libs/accesstransformer.cfg")

    runs {
        client = {
            workingDirectory project.file("run").canonicalPath
            source sourceSets.main

            mods {
                indicatia {
                    source sourceSets.main
                }
            }
        }
        server = {
            workingDirectory project.file("run").canonicalPath
            source sourceSets.main

            mods {
                indicatia {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"
    compile files("libs/SteveKunG's-Lib-${mc_version}-2.0.0-deobf.jar")
    embed("org.spongepowered:mixin:0.8-SNAPSHOT") { transitive = false }
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url "http://repo.spongepowered.org/maven/"
    }
}

mixin {
    add sourceSets.main, "mixins.indicatia.refmap.json"
}

jar {
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/*.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    manifest {
        attributes("MixinConnector": "com.stevekung.indicatia.core.IndicatiaMixinConnector"
        )
    }
    baseName = "Indicatia-" + mcVersion
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
	baseName = "Indicatia-" + mcVersion
    classifier = "sources"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/*.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    manifest {
        attributes("MixinConnector": "com.stevekung.indicatia.core.IndicatiaMixinConnector"
        )
    }
    baseName = "Indicatia-" + mcVersion
    classifier = "deobf"
}

artifacts {
    archives sourcesJar
    archives deobfJar
}

curseforge {
    apiKey = project.indicatia_api
    project {
        id = "272146"
        changelog = "Change Log"
        changelogType = "text"
        releaseType = "beta"
        addGameVersion mcVersion

        mainArtifact(jar) {
            displayName = "Indicatia-" + mcVersion + "-" + version
            relations {
                requiredLibrary "stevekungs-lib"
            }
        }
        addArtifact sourcesJar
        addArtifact deobfJar
    }
}

task signJar(type: SignJar, dependsOn: jar) {
    keyStore = project.keystore_path
    alias = project.alias
    storePass = project.keystore_pass
    keyPass = project.keystore_pass
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar